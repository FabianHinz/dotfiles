#
# User configuration sourced by login shells
#

# Initialize Zim
zimfw login-init -q &!


# Paths
# =====

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that Zsh searches for programs.
path=(
  ~/.bin
  ~/.local/{bin,sbin}
  /usr/local/{bin,sbin}
  $path
)

export GOPATH=${HOME}/projects/go

path=(
  ${GOPATH}/bin
  $path
)

if [ "$(uname -s)" = "Darwin" ]; then
  path=(
    $(brew --prefix coreutils)/libexec/gnubin
    $(brew --prefix python)/libexec/bin
    $path
  )
fi

# Less
# ====

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-g -i -M -R -S -w -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# Temporary Files
# ===============

if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$LOGNAME"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"
