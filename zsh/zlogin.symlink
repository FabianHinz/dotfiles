#
# startup file read in interactive login shells (after zshrc)
#


# Paths
# =====

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that Zsh searches for programs.
path=(
  ~/.bin
  ~/.local/{bin,sbin}
  /usr/local/{bin,sbin}
  $path
)

export GOPATH=${HOME}/projects/go
export PATH=${GOPATH}/bin:${PATH}

# Less
# ====

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# Temporary Files
# ===============

if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$LOGNAME"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"

# The following code helps us by optimizing the existing framework.
# This includes zcompile, zcompdump, etc.
#

(
  # Function to determine the need of a zcompile. If the .zwc file
  # does not exist, or the base file is newer, we need to compile.
  # These jobs are asynchronous, and will not impact the interactive shell
  zcompare() {
    if [[ -s ${1} && ( ! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
      zcompile ${1}
    fi
  }

  zim_mods=${ZDOTDIR:-${HOME}}/.zim/modules
  setopt EXTENDED_GLOB

  # zcompile the completion cache; siginificant speedup.
  for file in ${ZDOTDIR:-${HOME}}/.zcomp^(*.zwc)(.); do
    zcompare ${file}
  done

  # zcompile .zshrc
  zcompare ${ZDOTDIR:-${HOME}}/.zshrc

  # zcompile some light module init scripts
  zcompare ${zim_mods}/git/init.zsh
  zcompare ${zim_mods}/utility/init.zsh
  zcompare ${zim_mods}/pacman/init.zsh
  zcompare ${zim_mods}/spectrum/init.zsh
  zcompare ${zim_mods}/completion/init.zsh
  zcompare ${zim_mods}/fasd/init.zsh

  # zcompile all .zsh files in the custom module
  for file in ${zim_mods}/custom/**/^(README.md|*.zwc)(.); do
    zcompare ${file}
  done

  # zcompile all autoloaded functions
  for file in ${zim_mods}/**/functions/^(*.zwc)(.); do
    zcompare ${file}
  done

  # syntax-highlighting
  for file in ${zim_mods}/syntax-highlighting/external/highlighters/**/*.zsh; do
    zcompare ${file}
  done
  zcompare ${zim_mods}/syntax-highlighting/external/zsh-syntax-highlighting.zsh

  # zsh-histery-substring-search
  zcompare ${zim_mods}/history-substring-search/external/zsh-history-substring-search.zsh
  

) &!

# Is the Internet on fire?
# ========================
if [[ -t 0 || -t 1 ]]; then
  print 'Is the Internet on fire?\n========================\n\n'
  dig +short txt istheinternetonfire.com | sed -e 's/\\; /\n/' -e 's/"//'g
  print
fi
